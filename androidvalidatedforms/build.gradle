buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'net.vrallev.gradle:jarjar-gradle:1.1.0'
    }
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'ant', module: 'ant'
    all*.exclude group: 'awtui', module: 'awtui'
    all*.exclude group: 'swingui', module: 'swingui'
}

apply plugin: 'net.vrallev.jarjar'
apply plugin: 'com.android.library'
apply plugin: 'signing'
apply plugin: 'com.github.dcendents.android-maven'

repositories {
    jcenter()
    mavenCentral()
}

version "1.2.0"
group = "com.azavea.androidvalidatedforms"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    // Must specify individually all duplicated files from libraries to exclude or pickFirst
    packagingOptions {
        exclude 'NOTICE'
        exclude 'NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/DEPENDENCIES.txt'
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
    }

    lintOptions {
        abortOnError false
    }
    sourceSets { main { resources.srcDirs = ['src/main/resources', 'loc/'] } }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    configurations {
        all*.exclude group: 'junit', module: 'junit'
        all*.exclude group: 'commons-logging', module: 'commons-logging'
    }

    // hibernate dependencies missing on android; use jarjar to include on javax
    jarjar 'javax.xml.bind:jaxb-api:2.2.12'
    jarjar 'javax.xml.stream:stax-api:1.0-2'

    // validator -- use latest version still on Java 7
    jarjar('org.hibernate:hibernate-validator:5.1.3.Final') {
        exclude module: 'test-utils'
        exclude module: 'tck-runner'
        exclude module: 'intergration'
        exclude module: 'annotation-processor'
        exclude module: 'performance'
        exclude module: 'distribution'

        exclude group: 'javax.validation', module: 'validation-api'
    }

    // validation dependencies
    jarjar 'com.fasterxml:classmate:1.0.0'
    jarjar 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
    jarjar 'javax.el:javax.el-api:2.2.4'
    jarjar 'org.glassfish.web:javax.el:2.2.6'
    jarjar 'javax.enterprise:cdi-api:1.2'
    jarjar 'javax.xml.bind:activation:1.0.2'

    compile 'javax.validation:validation-api:1.1.0.Final'

    // Android support library
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:appcompat-v7:24.2.1'

    // Islamic calendar date picker
    compile('com.github.azavea:HijriDatePicker:1.3.9') {
        exclude module: 'example'
        exclude module: 'app'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives sourcesJar
}

if (project.hasProperty('signing')) {
    signing {
        required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }
}

install.dependsOn(assemble)

jarjar {
    // required, path to the executable .jar file
    jarJarFile 'tools/jarjar-1.4.jar'

    // optional, the rules for the jarjar tool
    rules = [
            'rule javax.xml.stream.** ext.javax.xml.stream.@1',
            'rule javax.xml.bind.** ext.javax.xml.bind.@1'
    ]

    // optional, exclude files from the dependency .jar files
    srcExcludes = ['META-INF/**']

    // optional, default is build_repackaged.jar, the result .jar file name
    outputName "build_repackaged.jar"

    // optional, default is libs, the directory of the result .jar
    outputDir 'libs'

    // optional, default is false
    ignoreJarJarResult false
}
